#|
This file is a part of rsbag-renderer
Author: Nicolas Hafner <shinmera@tymoon.eu>
|#

(in-package #:rsbag-renderer)
(defpackage #:rsbag-renderer.transform.haptic-vicon-3d
  (:use #:cl #:rsbag-renderer)
  (:export #:haptic-vicon-3d))
(in-package #:rsbag-renderer.transform.haptic-vicon-3d)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (rsbag-helper:load-proto-file (asdf:system-relative-pathname :rsbag-renderer "proto/Vicon.proto"))
  (rsbag-helper:load-proto-file (asdf:system-relative-pathname :rsbag-renderer "proto/Haptic.proto")))

(defparameter *vicon-haptic-map*
  (let ((map (make-hash-table :test 'equalp)))
    (loop for (k v) in '(("subject-4-baby-1" 7)
                         ("subject-4-baby-2" 8)
                         ("subject-4-ring-1" 35)
                         ("subject-4-ring-2" 33)
                         ("subject-4-middle-1" 40)
                         ("subject-4-middle-2" 45)
                         ("subject-4-index-1" 18)
                         ("subject-4-index-2" 30)
                         ("subject-4-thumb-1" 23)
                         ("subject-4-thumb-2" 22))
          do (setf (gethash k map) v))
    map))

(defun vicon-haptic-channel (name)
  (gethash name *vicon-haptic-map*))

(defun rgb-hex (rgb)
  (declare (optimize speed))
  (destructuring-bind (r g b) rgb
    (declare (type (integer 0 255) r g b))
    (let ((rgb 0))
      (setf (ldb (byte 255 0) rgb) b)
      (setf (ldb (byte 255 8) rgb) g)
      (setf (ldb (byte 255 16) rgb) r)
      rgb)))

(defun hue-rgb (h)
  (declare (optimize speed)
           (type (integer 0 360) h))
  (let ((x (round (* (- 1 (abs (- (mod (/ h 60) 2) 1))) 255))))
    (cond ((<=   0 h  60) (list 255 x 0))
          ((<=  60 h 120) (list x 255 0))
          ((<= 120 h 180) (list 0 255 x))
          ((<= 180 h 240) (list 0 x 255))
          ((<= 240 h 300) (list x 0 255))
          ((<= 300 h 360) (list 255 0 x)))))

(defun intensity-hue (intensity)
  (declare (optimize speed)
           (type (integer 0 4095) intensity))
  (let ((mini 0) (maxi 4095)
        (minh 0) (maxh 360))
    (round
     (+ (* (/ (- intensity mini) (- maxi mini))
           (- maxh minh))
        minh))))

(defun find-type (type &rest args)
  (loop for arg in args
        when (typep arg type)
        return arg))

(define-transform haptic-vicon-3d (a b) ()
  "Basic specific transform tailored to haptic and vicon combined recordings towards a GL3DVisualizer."
  (declare (optimize speed))
  (let* ((a (payload a))
         (b (payload b))
         (vicon (find-type 'rst.devices.mocap:vicon a b))
         (haptic (find-type 'rst.devices.haptic:haptic a b))
         (points (rst.devices.mocap:vicon-points vicon))
         (channels (rst.devices.haptic:haptic-channels haptic)))
    (with-json-object ()
      (loop for point across points
            do (let* ((coords (rst.devices.mocap:vicon/marker-point-position point))
                      (name (rst.devices.mocap:vicon/marker-point-name point))
                      (channel (vicon-haptic-channel name)))
                 (with-json-value (name)
                   (with-json-object ()
                     (pair "X" (rst.math:vec3ddouble-x coords))
                     (pair "Y" (rst.math:vec3ddouble-y coords))
                     (pair "Z" (rst.math:vec3ddouble-z coords))
                     (when channel
                       (pair "color" (rgb-hex (hue-rgb (intensity-hue (aref channels channel)))))))))))))
